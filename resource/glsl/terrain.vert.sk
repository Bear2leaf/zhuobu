#version 300 es 
#define EDGE_MORPH_TOP 1
#define EDGE_MORPH_LEFT 2
#define EDGE_MORPH_BOTTOM 4
#define EDGE_MORPH_RIGHT 8
#define MORPH_REGION 0.3f


layout(location = 0) in vec3 a_position;
layout(location = 1) in int a_edge;
layout(location = 2) in vec2 a_offset;
layout(location = 3) in float a_scale;
uniform mat4 u_model;
uniform mat4 u_projection;
uniform mat4 u_viewInverse;
uniform float u_resolution;
uniform sampler2D u_textureDepth;

out vec2 v_texcoord;
out vec4 v_modelPosition;

 // Poor man's bitwise &
bool edgePresent(int edge) {
    int e = a_edge / edge;
    return 2 * (e / 2) != e;
}

 // At the edges of tiles morph the vertices, if they are joining onto a higher layer
float calculateMorph(vec2 p) {
    float morphFactor = 0.0f;
    if(edgePresent(EDGE_MORPH_TOP) && p.y >= 1.0f - MORPH_REGION) {
        float m = 1.0f - clamp((1.0f - p.y) / MORPH_REGION, 0.0f, 1.0f);
        morphFactor = max(m, morphFactor);
    }
    if(edgePresent(EDGE_MORPH_LEFT) && p.x <= MORPH_REGION) {
        float m = 1.0f - clamp(p.x / MORPH_REGION, 0.0f, 1.0f);
        morphFactor = max(m, morphFactor);
    }
    if(edgePresent(EDGE_MORPH_BOTTOM) && p.y <= MORPH_REGION) {
        float m = 1.0f - clamp(p.y / MORPH_REGION, 0.0f, 1.0f);
        morphFactor = max(m, morphFactor);
    }
    if(edgePresent(EDGE_MORPH_RIGHT) && p.x >= 1.0f - MORPH_REGION) {
        float m = 1.0f - clamp((1.0f - p.x) / MORPH_REGION, 0.0f, 1.0f);
        morphFactor = max(m, morphFactor);
    }

    return morphFactor;
}

vec2 calculateNoMorphNeighbour(vec2 position, float morphK) {
    vec2 fraction = fract(a_position.xz * u_resolution * 0.5f) * 2.0f / u_resolution;
    return position - fraction * morphK * a_scale;
}

void main() {

    vec2 origin = a_position.xz;
     // Morph between zoom layers
    float morphK = calculateMorph(origin);
    vec2 position = origin * a_scale + a_offset;
    position = calculateNoMorphNeighbour(position, morphK);
    position = clamp(position, -0.9f, 0.9f);
    v_texcoord = position * 0.5f + 0.5f;
    float height = texture(u_textureDepth, v_texcoord).r;
    height = height * 2.0f - 1.0f;
    gl_Position = u_projection * u_viewInverse * u_model * vec4(position.x, height, position.y, 1.0f);
    v_modelPosition = u_model * vec4(position.x, height, position.y, 1.0f);
}